import org.gradle.api.publish.maven.internal.publication.MavenPomInternal;
import org.gradle.api.publish.maven.internal.publisher.MavenPublicationCoordinates;
import static org.gradle.api.publish.plugins.PublishingPlugin.PUBLISH_TASK_GROUP;

plugins {
    // This adds tasks to auto close or release nexus staging repos
    // see https://github.com/gradle-nexus/publish-plugin/
    id "io.github.gradle-nexus.publish-plugin"
}


allprojects {
    group 'org.quartz-scheduler'
    version quartzVersion
}

subprojects {
    repositories {
        mavenCentral()
    }
}

allprojects {
    plugins.withType(JavaBasePlugin) {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(11)
            }
        }
    }

    plugins.withType(MavenPublishPlugin) {
        plugins.apply(SigningPlugin)
        signing {
            required { !version.endsWith("SNAPSHOT") }
            sign publishing.publications
        }

        publishing {
            configurations {
                provided
                runtimeClasspath.extendsFrom(provided)
            }
            publications {
                maven(MavenPublication) {
                    pom {
                        name = "$project.name"
                        url = 'https://www.quartz-scheduler.org/'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com:quartz-scheduler/quartz.git'
                            developerConnection = 'scm:git:ssh:git@github.com:quartz-scheduler/quartz.git'
                            url = 'https://github.com/quartz-scheduler/quartz'
                        }
                        developers {
                            developer {
                                name = "Terracotta Engineers"
                                email = "tc-oss@wwpdl.vnet.ibm.com"
                                organization = "Super iPaaS Integration LLC, an IBM Company"
                                organizationUrl = "http://www.quartz-scheduler.org/"
                            }
                        }
                    }

                    from components.java
                    pom.withXml {
                        asNode()
                                .dependencies
                                .dependency
                                .findAll { dependency ->
                                    // Patch up dependency scopes per expectations of maven users
                                    (dependency.groupId.text() == 'com.mchange' ||
                                            dependency.groupId.text() == 'com.zaxxer' ||
                                            dependency.artifactId.text() == 'slf4j-log4j12' ||
                                            dependency.groupId.text() == 'jakarta.xml' )
                                }
                                .each { dependency ->
                                    // Set scope value to compile.
                                    dependency.scope*.value = 'provided'
                                }
                    }
                }
            }
        }

        // for MANIFEST.MF
        jar {
            manifest {
                attributes(
                        "Implementation-Title": project.name,
                        "Implementation-Vendor-Id": project.group,
                        "Implementation-Version": project.version,
                        "Built-By": System.getProperty("user.name"),
                        "Built-JDK": System.getProperty("java.version"),
                        "Automatic-Module-Name": 'org.quartz',
                        "-removeheaders": 'Private-Package',
                        "Export-Package": 'org.quartz.*',
                        "Import-Package": '*;resolution:=optional'
                )
            }
        }

    }

    // Put pom.xml and pom.properties in the jar:
    project.afterEvaluate{p ->
        // module.json publishing confuses sonatype and it doesn't process the jar.
        tasks.withType(GenerateModuleMetadata) {
            enabled = false
        }
        tasks.withType(GenerateMavenPom) {pomTask ->
            MavenPublicationCoordinates coordinates = ((MavenPomInternal) pomTask.getPom()).getCoordinates()
            TaskProvider<WriteProperties> pomPropertiesTask = project.getTasks().register(pomTask.getName().replace("PomFile", "PomProperties"), WriteProperties.class, task -> {
                task.dependsOn(pomTask);
                task.setGroup(PUBLISH_TASK_GROUP);
                task.setOutputFile(new File(pomTask.getDestination().getParentFile(), "pom.properties"));
                task.property("groupId", coordinates.getGroupId());
                task.property("artifactId", coordinates.getArtifactId());
                task.property("version", coordinates.getVersion());
            });

            project.tasks.withType(Jar).configureEach{jar ->
                jar.into("META-INF/maven/" + coordinates.getGroupId().get() + "/" + coordinates.getArtifactId().get(), spec -> {
                    spec.from(pomTask, pom -> pom.rename(".*", "pom.xml"));
                    spec.from(pomPropertiesTask);
                });
            }
        }
    }

}

nexusPublishing {
    repositories {
        sonatype()
    }
}
